// https://tech.excite.co.jp/entry/2021/12/01/120000

plugins {
	id 'org.springframework.boot' version '2.7.3'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'java'
}

allprojects {

    repositories {
        mavenCentral()
    }
}


subprojects {
    apply plugin: "java"

    group = 'com.juniordevmind'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '18'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-amqp:2.7.3'
        implementation 'org.springframework.boot:spring-boot-starter-web:2.7.3'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.3'
        implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.3'
        implementation 'mysql:mysql-connector-java:8.0.30'
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
        implementation 'org.modelmapper:modelmapper:3.1.0'
    }

    // https://stackoverflow.com/questions/67663728/spring-boot-2-5-0-generates-plain-jar-file-can-i-remove-it
    jar {
        enabled = false
    }

    def isMainProject = !['shared'].contains(project.name)

    if(isMainProject) {
        apply plugin: "org.springframework.boot"
        apply plugin: "io.spring.dependency-management"

        dependencies {
            testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.3'
            testImplementation 'org.springframework.amqp:spring-rabbit-test:2.4.6'
            developmentOnly 'org.springframework.boot:spring-boot-devtools:2.7.3'
        }

        tasks.named('test') {
	        useJUnitPlatform()
        }
    }
}


project(":author-api") {
    bootJar {
        enabled = true
    }

    dependencies {
        implementation project(':shared')
    }
}

project(":book-api") {
    bootJar {
        enabled = true
    }

    dependencies {
        implementation project(':shared')
    }
}

project(":comment-api") {
    bootJar {
        enabled = true
    }

    dependencies {
        implementation project(':shared')
    }
}

project(":shared") {
    // https://stackoverflow.com/questions/57662024/gradle-building-spring-boot-library-without-main-class
    bootJar {
        enabled = false
    }

    // https://qiita.com/opengl-8080/items/6409669dc3f8b8071af4
    jar {
        enabled = true
    }

    dependencies {
        
    }
}
